'*****************************************************
'*                                                   *
'*               如何启动默认拨号连接?               *
'*                    作者：栾金奎                   *
'*                     1999.11.11                    *
'*     下载网址：(163) http://howto.wojia.com        *
'*               (169) http://howto.bentium.net      *
'*                                                   *
'*****************************************************

Option Explicit

'有关注册的API
Private Declare Function RegOpenKeyEx Lib "advapi32" Alias "RegOpenKeyExA" (ByVal hKey As Long, ByVal lpSubKey As String, ByVal ulOptions As Long, ByVal samDesired As Long, phkResult As Long) As Long
Private Declare Function RegQueryValueEx Lib "advapi32" Alias "RegQueryValueExA" (ByVal hKey As Long, ByVal lpValueName As String, ByVal lpReserved As Long, ByRef lpType As Long, ByVal szData As String, ByRef lpcbData As Long) As Long
Private Declare Function RegCloseKey Lib "advapi32" (ByVal hKey As Long) As Long
'常数
Const HKEY_CURRENT_USER = &H80000001
Const ERROR_SUCCESS = 0&

Private Sub cmdCallConnect_Click()
    Shell "rundll rnaui.dll,RnaDial " + GetConnect, vbNormalFocus
End Sub

Public Function GetConnect() As String

    Dim hKey As Long
    Dim SubKey As String
    hKey = HKEY_CURRENT_USER
    SubKey = "RemoteAccess"
    GetConnect = GetRegValue(hKey, SubKey, "Default")

End Function
Public Function GetRegValue(hKey As Long, lpszSubKey As String, szKey As String) As Variant

    On Error GoTo ErrorRoutineErr:
    
    Dim phkResult As Long
    Dim lResult As Long
    Dim szBuffer As String
    Dim lBuffSize As Long

    '创建缓冲区
    szBuffer = Space(255)
    lBuffSize = Len(szBuffer)

    '打开键值
    RegOpenKeyEx hKey, lpszSubKey, 0, 1, phkResult

    '查询结果
    lResult = RegQueryValueEx(phkResult, szKey, 0, 0, szBuffer, lBuffSize)

    '关闭键
    RegCloseKey phkResult

    '返回结果值
    If lResult = ERROR_SUCCESS Then
        GetRegValue = Left(szBuffer, lBuffSize - 1)
    Else
        GetRegValue = ""
    End If
    Exit Function

ErrorRoutineErr:
    GetRegValue = ""
End Function

Private Sub Form_Load()

End Sub
